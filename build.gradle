import org.gradle.internal.os.OperatingSystem
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
  id 'java-library'
  id 'eclipse'
  id 'maven-publish'
  id 'jacoco'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
  id 'pl.allegro.tech.build.axion-release' version '1.13.14'
}

group = 'com.nordstrom.ui-tools'
description = 'Selenium Foundation'

if (!project.hasProperty('profile')) {
  ext.profile = 'selenium4'
} else {
  assert ['selenium3', 'selenium4'].contains(profile)
}

apply from: "${profile}Deps.gradle"

if (project.hasProperty('browsers')) {
  ext.browsers.split(',').each {
    def browser = it.trim()
    if (browser) {
      apply from: "${browser}Deps.gradle"
    }
  }
}

// if "personality" is specified
if (project.hasProperty('personality')) {
  // store "injected" browser name setting
  System.setProperty('injected.selenium.browser.name', ext.personality)
  // if target is a mobile browser
  if ([".chrome", ".safari"].any { ext.personality.endsWith(it) }) {
    // specify "web-app" as the injected default context platform
    System.setProperty('injected.selenium.context.platform', 'web-app')
    // ensure ExamplePageServlet is installed
    System.setProperty('injected.selenium.grid.examples', 'true')
  }
}

def archiveVer = null
def verBits = scmVersion.version.split('-')
def seleniumApi = 's' + profile.charAt(8)
if (verBits.length > 1) {
  if (verBits[1].equals(seleniumApi)) {
    archiveVer = project.version
  } else {
    archiveVer = verBits[0] + '-' + seleniumApi + '-' + verBits[1]
  }
} else {
  archiveVer = verBits[0] + '-' + seleniumApi
}

project.version = archiveVer
  
def archiveBase = rootProject.name + '-' + archiveVer

java {
  withJavadocJar()
  withSourcesJar()
}

clean {
  delete 'logs'
  delete 'target'
}

jacoco {
  toolVersion = '0.8.8'
  reportsDirectory = file("${buildDir}/customJacocoReportDir")
}

//jacocoTestReport {
//  reports {
//    xml.enabled false
//    csv.enabled false
//    html.destination file("${buildDir}/jacocoHtml")
//  }
//}

jar {
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' classes, POM and Maven properties."
  
  def destPath = "META-INF/maven/${project.group}/${rootProject.name}"
  def timestamp = Long.valueOf(System.currentTimeMillis()).toString()
  def pomTokens = [projectVersion: archiveVer, projectTimestamp: timestamp, seleniumApi: seleniumApi]
  def propTokens = [projectVersion: archiveVer, projectGroupId: project.group, projectArtifactId: rootProject.name]
  
  from('.') {
    include('pom.xml')
    into(destPath)
    filter(ReplaceTokens, tokens: pomTokens)
  }
  from('.') {
    include('pom.properties')
    into(destPath)
    filter(ReplaceTokens, tokens: propTokens)
  }
  archiveFileName = archiveBase + '.jar'
  destinationDirectory = libsDir
}

clean {
  delete buildRoot
}

sourcesJar {
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' source files."
  
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
  archiveFileName = archiveBase + '-sources.jar'
  destinationDirectory = libsDir
}

javadocJar {
  dependsOn javadoc
  group 'Build'
  description "Assembles a jar archive containing the '${profile}' JavaDoc files."
  
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
  archiveFileName = archiveBase + '-javadoc.jar'
  destinationDirectory = libsDir
}

tasks.withType(Test).configureEach {
  // iterate over system properties
  System.getProperties().each { key, val ->
    // if this is a property that should be propagated to the test task
    if (key.endsWith('.binary.path') || ['injected.', 'selenium.', 'appium.', 'testng.', 'junit.'].any { key.startsWith(it) }) {
      // if running on Windows and this property defines JSON browser capabilities
      if (OperatingSystem.current().isWindows() && key.endsWith('selenium.browser.caps')) {
        // escape double quotes
        val = val.replace('"', '\\\"')
      }
      // propagate property
      systemProperty key, val
    }
  }
}


task testNG(type: Test) {
  useTestNG()
  reports.html.destination = file("${buildDir}/reports/testng")
  testLogging.showStandardStreams = true
  
  // --- Optional test JVM debugging ---
  if (project.findProperty('debugTestNG') == 'true') {
    jvmArgs += [
      '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005'
    ]
    maxParallelForks = 1
    forkEvery = 0
  }
}

test {
  dependsOn testNG
  reports.html.destination = file("${buildDir}/reports/junit")
  testLogging.showStandardStreams = true
  
  // --- Optional test JVM debugging ---
  if (project.findProperty('debugJUnit') == 'true') {
    jvmArgs += [
      '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006'
    ]
    maxParallelForks = 1
    forkEvery = 0
  }
}

scmVersion {
  hooks {
    pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /(&lt;version&gt;)\d+\.\d+\.\d+(-s[34]&lt;\/version&gt;)/}, replacement: {v, p -> "\$1$v\$2"}]
    pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /(selenium-foundation:)\d+\.\d+\.\d+(-s[34])/}, replacement: {v, p -> "\$1$v\$2"}]
    pre 'commit'
    post 'push'
  }
}

artifacts {
  archives tasks['sourcesJar']
  archives tasks['javadocJar']
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      pom {
        name = 'Selenium Foundation'
        groupId = project.group
        artifactId = rootProject.name
        version = archiveVer
        packaging = 'jar'
        description = 'Selenium Foundation is an automation framework designed to extend and enhance the capabilities provided by Selenium (WebDriver).'
        url = 'https://github.com/sbabcoc/Selenium-Foundation'

        scm {
          connection = 'scm:git:https://github.com/sbabcoc/Selenium-Foundation.git'
          developerConnection = 'scm:git:https://github.com/sbabcoc/Selenium-Foundation.git'
          url = 'https://github.com/sbabcoc/Selenium-Foundation/tree/master'
          tag = 'HEAD'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'scoba'
            name = 'Scott Babcock'
            email = 'scoba@hotmail.com'
            organization = 'Nordstrom'
            organizationUrl = 'https://shop.nordstrom.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

task install {
  dependsOn publishToMavenLocal
  group publishToMavenLocal.group
  description '[alias] ' + publishToMavenLocal.description
}

nexusPublishing {
  packageGroup = 'com.nordstrom'
  repositories {
    ossrh {
      nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
      snapshotRepositoryUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"))
      if (project.hasProperty('ossrhStagingProfileId')) {
        stagingProfileId = ossrhStagingProfileId
      }
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://repo1.maven.org/maven2' }
  maven { url 'https://repo.maven.apache.org/maven2' }
  maven { url "${projectDir}/repo" }
}

dependencies {
  constraints {
    api 'com.nordstrom.tools:java-utils:3.4.1'
    api 'com.nordstrom.tools:settings:3.0.8'
    api 'com.nordstrom.tools:junit-foundation:17.2.4'
    api 'com.github.sbabcoc:logback-testng:2.0.1'
    api 'org.hamcrest:hamcrest-core:3.0'
    api 'org.yaml:snakeyaml:2.4'
  }
  api 'com.nordstrom.tools:java-utils'
  api 'com.nordstrom.tools:settings'
  api 'com.nordstrom.tools:junit-foundation'
  api('com.github.sbabcoc:logback-testng') {
    exclude group: 'org.testng', module: 'testng'
  }
  api 'org.hamcrest:hamcrest-core'
  api 'org.yaml:snakeyaml'
}

test {
  jvmArgs = [
    "-javaagent:${classpath.find { it.name.contains('junit-foundation') }.absolutePath}"
  ]
}
